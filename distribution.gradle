/**
 * Generates project changelog using github-changelog-generator
 * https://github.com/github-changelog-generator/github-changelog-generator
 */
tasks.register("generateChangelog", Exec.class) {
    it.description('Generate a project changelog.')
    it.group('documentation')

    def commandArgs = [
            // owner of target GitHub repository
            '-u', 'real-coco-labs',
            /*
             * name of project on GitHub */
            '-p', project.name,
            /*
             * only issues with the specified labels will be included in the changelog
             * this means that issues that are included HAVE to have at least one of these labels */
            '--include-labels', 'bug,enhancement,breaking,deprecated',
            /*
             * define all labels you want excluded from the changelog */
            '--exclude-labels', 'dev,wontfix,workflow,question,documentation',
    ]
    // Changelog will start after specified tag
    if (project.hasProperty('cg_sinceTag')) {
        commandArgs += [ '--since-tag', project.ext.cg_sinceTag ]
    }
    // Output file. To print to STDOUT instead, use blank as path.
    // Default value: CHANGELOG.md
    if (project.hasProperty('cg_output')) {
        commandArgs += [ '--output', project.ext.cg_output ]
    }
    // Generate log from unreleased closed issues only or
    // add to log unreleased closed issues.
    // Default value: true
    if (project.hasProperty('cg_unreleased')) {
        commandArgs += [Boolean.parseBoolean(project.ext.cg_unreleased as String) ? '--unreleased' : '--no-unreleased']
    }
    else  commandArgs += ['--no-unreleased' ]
    // Set up custom header label. Default is "# Changelog".
    // Default value is #CHANGELOG
    if (project.hasProperty('cg_header')) {
        commandArgs += [ '--header-label', project.ext.cg_header ]
    }
    // generator will first check for token in environment variables
    if (!providers.environmentVariable('CHANGELOG_GITHUB_TOKEN').forUseAtConfigurationTime().present)
    {
        def githubToken = project.ext.has('githubToken') ? project.ext.githubToken : null
        if (githubToken != null) {
            commandArgs += [ '--token', githubToken ]
        }
    }
    def command = [ 'bundle', 'exec', 'github_changelog_generator' ] + commandArgs
    it.commandLine = (project.ext.osName == "Windows") ? [ 'cmd', '/c' ] + command : command
    it.doFirst {
        logger.info('Generating changelog with arguments: ' + commandArgs)
    }
}

distributions.main.contents {
    it.from ('README.md', 'LICENSE.txt', 'CHANGELOG.md')
    it.exclude {
        File file = it.file
        String filename = file.getName()
        /*
         * there is a bug that occurs when running distribution tasks on Github CI
         * where the distribution archive tries to add itself to archive
         * @see https://github.com/real-coco-labs/pz-zdoc/issues/21
         */
        if (filename.startsWith(archivesBaseName) && filename.endsWithAny('.zip', '.tar')) {
            return true
        }
        if (!CI) {
            // exclude Project Zomboid classes
            if (file.toPath().startsWith(zomboidClassesDir.toPath())) {
                return true
            }
            // exclude Project Zomboid libraries
            if (file.toPath().startsWith(gameDir)) {
                return true
            }
        }
        return false
    }
}
// generate changelog BEFORE creating distribution
[ 'assemble', 'assembleDist', 'installDist' ].forEach({
    tasks.named(it).configure({it.dependsOn('generateChangelog')})
})
// run distribution archive tasks AFTER generating changelog
[ 'distTar', 'distZip' ].forEach({
    tasks.named(it).configure({it.mustRunAfter('generateChangelog')})
})
def cleanScripts = tasks.register('cleanScripts', Delete.class) {
    it.description('Delete build scripts directory.')
    it.group('zomboid')
    it.delete("$buildDir/scripts")
}
tasks.named('installDist').configure{
    it.dependsOn(cleanScripts)
}

tasks.named('startScripts').configure {
    it.setWindowsStartScriptGenerator(new ZWindowsStartScriptGenerator())
    it.setUnixStartScriptGenerator(new ZUnixStartScriptGenerator())

    // do not add provided files to classpath, they are not included in distribution
    it.classpath -= configurations.zomboidImplementation
}